plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "eclipse"
}

sourceCompatibility = 1.8

version = "1.1.0." + ('git rev-list --count HEAD'.execute().text.trim()) + "-1.12.2"
group = "net.buildtheearth"


configurations {
    shade
    compile.extendsFrom shade
}

repositories {
    maven { //used for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { //used for leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
}

dependencies {
    shade "org.apache.commons:commons-imaging:1.0-alpha2"
    shade "commons-codec:commons-codec:1.15"
    shade "org.apache.commons:commons-lang3:3.12.0"
    shade "it.unimi.dsi:fastutil:8.5.2"

    shade("net.daporkchop.lib:binary:0.5.5-SNAPSHOT")  {
        exclude group: "io.netty"
    }
    
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile "com.fasterxml.jackson.core:jackson-databind:2.11.2"  
    
    shade "com.google.guava:guava:30.1-jre"
    shade "io.netty:netty-all:4.1.9.Final"
    
    shade group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    shade group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    
    implementation group: 'lzma', name: 'lzma', version: '0.0.1'
    
    
    testImplementation "junit:junit:4.12"

    compileOnly "org.projectlombok:lombok:1.18.16"
    annotationProcessor "org.projectlombok:lombok:1.18.16"
}


shadowJar {
    classifier = null
    configurations = [project.configurations.shade]

    exclude 'module-info.class'
}
build.dependsOn shadowJar

//relocate all shaded dependencies
task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "net.buildtheearth.terraminusminus.dep"
}
shadowJar.dependsOn relocateShadowJar

